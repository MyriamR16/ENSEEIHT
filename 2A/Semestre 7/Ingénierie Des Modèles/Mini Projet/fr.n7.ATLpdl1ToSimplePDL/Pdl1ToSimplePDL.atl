-- @path SPDL=/fr.n7.simplePDL/SimplePDL.ecore
-- @path XPDL=/fr.n7.pdl1/model/generated/PDL1.ecore
module Pdl1ToSimplePDL;

-- ... l'en-tête est volontairement omis
-- Note : XPDL = méta-modèle issu de Xtext
--        SPDL = méta-modèle SimplePDL
create OUT : SPDL from IN : XPDL;

-- Ce helper n'a pas de contexte. Il est défini au niveau du module.
helper def: convertLinkType(x : XPDL!WorkSequenceType) : SPDL!WorkSequenceType =
    if x = #start2start then #startToStart
    else if x = #start2finish then #startToFinish
    else if x = #finish2start then #finishToStart
    else #finishToFinish
    endif endif endif; -- pas de SinonSi en ATL

rule XProc2SProc {
    from xp : XPDL!Process
    to   sp : SPDL!Process(
        name <- xp.name,
	-- Ajouter les enfants, sinon les autres éléments créés seront orphelins !
        processElements <- xp.processElements)
}

rule XWD2SWD {
    from xwd : XPDL!WorkDefinition
    to   swd : SPDL!WorkDefinition(name <- xwd.name)
}

rule XWS2SWS {
    from xws : XPDL!WorkSequence
    to   sws : SPDL!WorkSequence(
            linkType <- thisModule.convertLinkType(xws.linkType), -- Appel à notre helper
            predecessor <- xws.predecessor, -- Conversion/appel de règle implicite
            successor <- xws.successor
        )
}

rule XRESS2SRESS {
    from xress : XPDL!Ressource
    to   sress : SPDL!Ressource(
    	name <- xress.name,
		quantite <- xress.quantite
    	)
}

rule XOCC2SOCC {
    from xocc : XPDL!Occurrence
    to   socc : SPDL!Occurrence(
		occurrence <- xocc.occurrence,
		workDefinition <- xocc.wdAssociee,
		ressource <- xocc.ressourceUtile
    	)
}